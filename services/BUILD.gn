# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/request/request/request_aafwk.gni")
import("//build/ohos.gni")

rust_cxx("download_server_cxx_gen") {
  sources = [
    "src/manage/account.rs",
    "src/manage/database.rs",
    "src/manage/network.rs",
    "src/manage/query.rs",
    "src/service/notification_bar/mod.rs",
    "src/task/bundle.rs",
    "src/task/config.rs",
    "src/task/info.rs",
    "src/task/reason.rs",
    "src/utils/common_event.rs",
    "src/utils/mod.rs",
  ]
}

config("download_server_cxx_config") {
  include_dirs = [
    "include",
    "${target_gen_dir}/src",
    "../common/sys_event/include",
  ]
}

ohos_static_library("download_server_cxx") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  stack_protector_ret = true

  public_configs = [ ":download_server_cxx_config" ]

  sources = [
    "src/cxx/account.cpp",
    "src/cxx/application_state_observer.cpp",
    "src/cxx/bundle.cpp",
    "src/cxx/c_request_database.cpp",
    "src/cxx/c_string_wrapper.cpp",
    "src/cxx/c_task_config.cpp",
    "src/cxx/c_task_info.cpp",
    "src/cxx/common_event.cpp",
    "src/cxx/get_proxy.cpp",
    "src/cxx/network.cpp",
    "src/cxx/notification_bar.cpp",
    "src/cxx/request_cert_mgr_adapter.cpp",
    "src/cxx/request_utils.cpp",
    "src/cxx/url_policy.cpp",
  ]
  sources += get_target_outputs(":download_server_cxx_gen")

  external_deps = [
    "ability_base:base",
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_manager",
    "ability_runtime:app_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "certificate_manager:cert_manager_sdk",
    "common_event_service:cesfwk_innerkits",
    "distributed_notification_service:ans_innerkits",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "i18n:intl_util",
    "ipc:ipc_single",
    "netmanager_base:net_conn_manager_if",
    "os_account:libaccountkits",
    "os_account:os_account_innerkits",
    "relational_store:native_appdatafwk",
    "relational_store:native_dataability",
    "relational_store:native_rdb",
    "resource_management:global_resmgr",
    "rust_cxx:cxx_cppdeps",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "ylong_runtime:ylong_runtime",
    "ability_runtime:wantagent_innerkits",
  ]

  deps = [
    ":download_server_cxx_gen",
    "../common/sys_event:request_sysevent",
    "../etc/icon:request_notificaton_xmark_icon",
  ]

  defines = []

  if (target_platform == "watch") {
    defines += [ "REQUEST_DEVICE_WATCH" ]
  }

  if (request_telephony_core_service && request_telephony_cellular_data) {
    external_deps += [
      "cellular_data:tel_cellular_data_api",
      "core_service:tel_core_service_api",
    ]
    defines += [ "REQUEST_TELEPHONY_CORE_SERVICE" ]
  }

  subsystem_name = "request"
  part_name = "request"
}

ohos_rust_shared_library("download_server") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [ "src/lib.rs" ]

  features = [ "oh" ]

  deps = [
    ":download_server_cxx",
    "../common/database:database_rs",
    "../common/utils:request_utils",
  ]

  external_deps = [
    "hilog:libhilog",
    "hilog:hilog_rust",
    "hisysevent:hisysevent_rust",
    "hitrace:hitrace_meter_rust",
    "ipc:ipc_rust",
    "netstack:ylong_http_client",
    "rust_cxx:lib",
    "safwk:system_ability_fwk_rust",
    "samgr:samgr_rust",
    "ylong_runtime:ylong_runtime",
  ]

  crate_name = "download_server"
  subsystem_name = "request"
  part_name = "request"
}
